apply plugin: 'maven-publish'

dependencies {
    compile project(path: ':b', configuration: 'myArchives')
}

sourceSets {
    main {
        groovy {
            srcDir 'src/main/groovy'
        }
    }
}

task myJar(type: Jar) {
    baseName = 'jar-from-a'
    from project.sourceSets.main.output
}

publishing {
    repositories {
        maven {
            url "http://kvs-us-factory.int.kronos.com:8081/artifactory/k08-00"
            credentials {
                username = "artifactoryuploader"
                password = "kronites"
            }
        }
    }

    publications.create(myJar.name, MavenPublication) {
        artifactId = myJar.baseName
        groupId = project.group
        artifact myJar
        
        addAllProjectDependenciesToPom(pom, configurations.runtime)
    }
}

def addAllProjectDependenciesToPom(pom, Configuration configuration){
    pom.withXml { provider ->
        def node = provider.asNode()
        def dependenciesNode = node.dependencies
        if(!dependenciesNode) {
            dependenciesNode = node.appendNode('dependencies')
        }
        configuration.allDependencies.withType(ModuleDependency).each{ moduleDep ->
            if(moduleDep instanceof ProjectDependency) {
                moduleDep.getProjectConfiguration().getAllArtifacts().each { artifact ->
                    def artifactId = "${artifact.name}" + (artifact.classifier ? "-${artifact.classifier}" : "")
                    addDependencyNodeToPom(dependenciesNode, configuration.name, moduleDep.group, artifactId, moduleDep.version)
                }
            } else {
                addDependencyNodeToPom(dependenciesNode, configuration.name, moduleDep.group, moduleDep.name, moduleDep.version)
            }
        }
    }
}

def addDependencyNodeToPom(dependenciesNode, scope, group, artifact, version) {
    def dependencyNode = dependenciesNode.appendNode('dependency')
    dependencyNode.appendNode("scope", scope)
    dependencyNode.appendNode("groupId", group)
    dependencyNode.appendNode("artifactId", artifact)
    dependencyNode.appendNode("version", version)
}